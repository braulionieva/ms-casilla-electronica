CREATE OR REPLACE PACKAGE SISCAS.CAPK_AFILIADO
AS
-- Package header
PROCEDURE VALIDAR_USUARIO_CASILLA(
    PI_N_TIPO_DOCUMENTO IN VARCHAR2,
    PI_V_NUMERO_DOCUMENTO IN VARCHAR2,
    PO_N_ESTADO OUT NUMBER,
    PO_V_ERR_COD OUT VARCHAR2,
    PO_V_ERR_MSG OUT VARCHAR2
);


PROCEDURE REGISTRAR_NUEVO_AFILIADO (
    PI_N_ID_TIPO_DOC_IDENT 		IN SISCFEBF.CFTV_PERSONA.ID_N_TIPO_DOC_IDENT%TYPE,
    PI_V_NU_DOCUMENTO 			IN SISCFEBF.CFTV_PERSONA.NU_V_DOCUMENTO%TYPE,
    PI_V_USUARIO 				IN SISCAS.SITV_CASILLA.CO_V_USUARIO%TYPE,
    PI_N_ID_TIPO_PERSONA 		IN SISCAS.SITV_CASILLA.ID_N_TIPO_PERSONA%TYPE,
    PI_V_ES_ABOGADO				IN SISCAS.SITV_CASILLA.FL_C_ABOGADO%TYPE,
    PI_V_CORREO 				IN SISCAS.SITV_CASILLA.DE_V_CORREO%TYPE,
    PI_V_TELEFONO 				IN SISCAS.SITV_CASILLA.NU_V_TELEFONO%TYPE,
    PI_D_FE_AFILIACION 			IN SISCAS.SITV_CASILLA.FE_D_AFILIACION%TYPE,
    PI_V_ID_COLEGIO_ABOGADOS 	IN SISCFEBF.CFTV_ABOGADO.ID_N_COLEGIO_ABOGADOS%TYPE,
    PI_V_NU_COLEGIO 			IN SISCFEBF.CFTV_ABOGADO.NU_V_COLEGIO%TYPE,
    PI_ID_TABLA_DESCRIPCION		IN SISCAS.SITV_CASILLA.ID_V_TABLA_DESCRIPCION%TYPE,
    PO_V_PASSWORD 				OUT VARCHAR2,
    PO_V_ERR_COD 				OUT VARCHAR2,
    PO_V_ERR_MSG 				OUT VARCHAR2

);

PROCEDURE RECOVER_DATA(  PI_USUARIO IN VARCHAR2,
                         PO_V_ERR_COD OUT VARCHAR2,
    					PO_V_ERR_MSG OUT VARCHAR2,
                        PO_CURSOR OUT SYS_REFCURSOR);

PROCEDURE ACTUALIZAR_PASSWORD(  PI_USUARIO 		IN VARCHAR2,
								PI_NEW_PW 		IN VARCHAR2,
                         		PI_OLD_PW 		IN VARCHAR2,
                         		PO_V_ERR_COD 	OUT VARCHAR2,
    							PO_V_ERR_MSG 	OUT VARCHAR2
    							);

PROCEDURE ACTUALIZAR_CORREO(
		PI_V_CORREO 		IN VARCHAR2,
		PI_V_ID_CASILLA		IN VARCHAR2,
        PO_V_ERR_COD 		OUT VARCHAR2,
    	PO_V_ERR_MSG 		OUT VARCHAR2
    );
PROCEDURE ACTUALIZAR_DOBLE_PERFIL(
								PI_V_ID_CASILLA 		IN VARCHAR2,
								PI_V_ID_PERSONA 		IN VARCHAR2,
								PI_V_DOBLE_PERFIL		IN VARCHAR2,
                         		PI_N_TIPO_CASILLA		IN NUMBER,
								PI_V_ID_COLEGIO 		IN VARCHAR2,
                         		PI_N_NU_COLEGIO 		IN VARCHAR2,
                         		PO_V_ERR_COD 			OUT VARCHAR2,
    							PO_V_ERR_MSG 			OUT VARCHAR2
    							);
PROCEDURE CASP_USUARIOS_CON_PW_INICIAL(
				PO_CURSOR 				OUT SYS_REFCURSOR,
				PO_V_ERR_COD 			OUT VARCHAR2,
    			PO_V_ERR_MSG 			OUT VARCHAR2
    		);

END CAPK_AFILIADO;

CREATE OR REPLACE PACKAGE BODY SISCAS.CAPK_AFILIADO
AS

PROCEDURE VALIDAR_USUARIO_CASILLA(
    PI_N_TIPO_DOCUMENTO IN VARCHAR2,
    PI_V_NUMERO_DOCUMENTO IN VARCHAR2,
    PO_N_ESTADO OUT NUMBER,
    PO_V_ERR_COD OUT VARCHAR2,
    PO_V_ERR_MSG OUT VARCHAR2
) AS

BEGIN

    SELECT sc.ES_C_CASILLA INTO PO_N_ESTADO
    FROM SISCFEBF.CFTV_PERSONA cp
    INNER JOIN SISCAS.SITV_CASILLA sc ON sc.ID_V_PERSONA = cp.ID_V_PERSONA
    WHERE cp.NU_V_DOCUMENTO = PI_V_NUMERO_DOCUMENTO AND cp.ID_N_TIPO_DOC_IDENT = PI_N_TIPO_DOCUMENTO
    AND ROWNUM = 1;
    PO_V_ERR_COD := '0';

    PO_V_ERR_MSG := 'OK';
EXCEPTION
    WHEN NO_DATA_FOUND THEN
        PO_N_ESTADO := -1;
        PO_V_ERR_COD := '0';
        PO_V_ERR_MSG := 'No se encontró el usuario';
    WHEN OTHERS THEN
        PO_V_ERR_COD := '1';
        PO_V_ERR_MSG := SQLERRM || ' ' || SQLCODE;
END VALIDAR_USUARIO_CASILLA;


--Registrando nuevo afiliado
PROCEDURE REGISTRAR_NUEVO_AFILIADO (
    PI_N_ID_TIPO_DOC_IDENT 		IN SISCFEBF.CFTV_PERSONA.ID_N_TIPO_DOC_IDENT%TYPE,
    PI_V_NU_DOCUMENTO 			IN SISCFEBF.CFTV_PERSONA.NU_V_DOCUMENTO%TYPE,
    PI_V_USUARIO 				IN SISCAS.SITV_CASILLA.CO_V_USUARIO%TYPE,
    PI_N_ID_TIPO_PERSONA 		IN SISCAS.SITV_CASILLA.ID_N_TIPO_PERSONA%TYPE,
    PI_V_ES_ABOGADO				IN SISCAS.SITV_CASILLA.FL_C_ABOGADO%TYPE,
    PI_V_CORREO 				IN SISCAS.SITV_CASILLA.DE_V_CORREO%TYPE,
    PI_V_TELEFONO 				IN SISCAS.SITV_CASILLA.NU_V_TELEFONO%TYPE,
    PI_D_FE_AFILIACION 			IN SISCAS.SITV_CASILLA.FE_D_AFILIACION%TYPE,
    PI_V_ID_COLEGIO_ABOGADOS 	IN SISCFEBF.CFTV_ABOGADO.ID_N_COLEGIO_ABOGADOS%TYPE,
    PI_V_NU_COLEGIO 			IN SISCFEBF.CFTV_ABOGADO.NU_V_COLEGIO%TYPE,
    PI_ID_TABLA_DESCRIPCION		IN SISCAS.SITV_CASILLA.ID_V_TABLA_DESCRIPCION%TYPE,
    PO_V_PASSWORD 				OUT VARCHAR2,
    PO_V_ERR_COD 				OUT VARCHAR2,
    PO_V_ERR_MSG 				OUT VARCHAR2

) AS
    V_ID_V_PERSONA SISCFEBF.CFTV_PERSONA.ID_V_PERSONA%TYPE;
   	V_V_ID_CASILLA	SISCAS.SITV_CASILLA.ID_V_CASILLA%TYPE;
   	COUNT_ABOGADO NUMBER;
   	V_FE_ABOGADO SISCAS.SITV_CASILLA.FE_D_ABOGADO%TYPE := NULL;
BEGIN
    SELECT ID_V_PERSONA INTO V_ID_V_PERSONA
    FROM SISCFEBF.CFTV_PERSONA
    WHERE ID_N_TIPO_DOC_IDENT = PI_N_ID_TIPO_DOC_IDENT
      AND NU_V_DOCUMENTO = PI_V_NU_DOCUMENTO;

     IF PI_V_ES_ABOGADO = '1' THEN
     	V_FE_ABOGADO := SYSDATE;
     END IF;


	SELECT SYS_GUID() INTO V_V_ID_CASILLA  FROM DUAL;
	SELECT dbms_random.string('A', 10) INTO PO_V_PASSWORD FROM DUAL;
    INSERT INTO SISCAS.SITV_CASILLA (
    	ID_V_CASILLA,
        ID_V_PERSONA,
        ID_N_TIPO_PERSONA,
        FL_C_ABOGADO,
        CO_V_USUARIO,
        PW_V_USUARIO,
        DE_V_CORREO,
        NU_V_TELEFONO,
        ID_V_TABLA_DESCRIPCION,
        FE_D_AFILIACION,
        FE_D_CREACION,
        FE_D_MODIFICACION,
        FE_D_TYC,
        CO_V_US_CREACION,
        ES_C_CASILLA,
        FE_D_ABOGADO
    ) VALUES (
        V_V_ID_CASILLA,
        V_ID_V_PERSONA,
        PI_N_ID_TIPO_PERSONA,
        PI_V_ES_ABOGADO,
        PI_V_USUARIO,
        PKG_CE_SECURITY.encrypt_pw(PO_V_PASSWORD),
        PI_V_CORREO,
        PI_V_TELEFONO,
        PI_ID_TABLA_DESCRIPCION,
        PI_D_FE_AFILIACION,
        SYSDATE,
        SYSDATE,
        SYSDATE,
        'USRSIS',
        1, --CASILLA ACTIVA
        V_FE_ABOGADO
    );
   -- ABOGADO
   IF PI_V_ES_ABOGADO = '1' THEN
   		SELECT COUNT(*) INTO COUNT_ABOGADO
   		FROM SISCFEBF.CFTV_ABOGADO ca
   		WHERE ca.ID_V_PERSONA = V_ID_V_PERSONA;

   		IF COUNT_ABOGADO < 1 THEN
   			INSERT INTO SISCFEBF.CFTV_ABOGADO (
    			ID_N_COLEGIO_ABOGADOS,
    			ID_V_PERSONA,
    			NU_V_COLEGIO,
    			CO_V_US_CREACION
			)
			VALUES (
    			PI_V_ID_COLEGIO_ABOGADOS,
    			V_ID_V_PERSONA,
    			PI_V_NU_COLEGIO,
    			'USRSIS'
			);
		ELSE
				UPDATE SISCFEBF.CFTV_ABOGADO
					SET ID_N_COLEGIO_ABOGADOS=PI_V_ID_COLEGIO_ABOGADOS,
					NU_V_COLEGIO=PI_V_NU_COLEGIO
					WHERE ID_V_PERSONA=V_ID_V_PERSONA;
   		END IF;
   END IF;

   	PO_V_ERR_COD := '0';
    PO_V_ERR_MSG := 'OK';

   --- movimiento
			INSERT INTO SISCAS.SITV_MOVIMIENTO_CASILLA (
    			ID_V_CASILLA,
    			ID_N_TIPO_MOVIMIENTO_CASILLA,
    			DE_V_MOVIMIENTO_CASILLA,
    			CO_V_US_CREACION
			) VALUES (
    			V_V_ID_CASILLA,
    			5,
    			'CREACIÓN CASILLA',
    			PI_V_USUARIO
			);


EXCEPTION
    WHEN NO_DATA_FOUND THEN
    	PO_V_ERR_COD := '1';
        PO_V_ERR_MSG := 'No se encontraron datos para esa persona';
    WHEN OTHERS THEN
        PO_V_ERR_COD := '-1';
        PO_V_ERR_MSG := SQLERRM || ' ' || SQLCODE;
END REGISTRAR_NUEVO_AFILIADO;

PROCEDURE RECOVER_DATA(
						PI_USUARIO 		IN VARCHAR2,
                        PO_V_ERR_COD 	OUT VARCHAR2,
    					PO_V_ERR_MSG 	OUT VARCHAR2,
                        PO_CURSOR 		OUT SYS_REFCURSOR
                        ) AS

 	V_ID_N_TIPO_PERSONA	 	SISCAS.SITV_CASILLA.ID_N_TIPO_PERSONA%TYPE;
 	V_ID_PERSONA 			SISCFEBF.CFTV_PERSONA.ID_V_PERSONA%TYPE;
	V_ES_CASILLA 			SISCAS.SITV_CASILLA.ES_C_CASILLA%TYPE;
	V_ID_CASILLA 			SISCAS.SITV_CASILLA.ID_V_CASILLA%TYPE;
	V_DE_CORREO 			SISCAS.SITV_CASILLA.DE_V_CORREO%TYPE;
	V_NU_TELEFONO 			SISCAS.SITV_CASILLA.NU_V_TELEFONO%TYPE;
	V_NU_DOCUMENTO 			SISCFEBF.CFTV_PERSONA.NU_V_DOCUMENTO%TYPE;
	V_FL_PRIMER_LOGIN 		SISCAS.SITV_CASILLA.FL_C_PRIMER_LOGIN%TYPE;
	V_FL_C_ABOGADO 			SISCAS.SITV_CASILLA.FL_C_ABOGADO%TYPE;
	V_CO_USUARIO 			SISCAS.SITV_CASILLA.CO_V_USUARIO%TYPE;
	V_FL_C_DEFENSOR			SISCAS.SITV_CASILLA.FL_C_DEFENSOR_PUBLICO%TYPE;
 BEGIN
	PO_V_ERR_MSG := 'OK';

SELECT
	SC.ID_V_CASILLA,
	sc.ES_C_CASILLA,
	SC.ID_N_TIPO_PERSONA,
	SC.DE_V_CORREO,
	SC.NU_V_TELEFONO,
	CP.NU_V_DOCUMENTO,
	CP.ID_V_PERSONA,
	SC.FL_C_PRIMER_LOGIN,
	SC.FL_C_ABOGADO,
	SC.CO_V_USUARIO,
	SC.FL_C_DEFENSOR_PUBLICO
	 INTO
	V_ID_CASILLA,
	V_ES_CASILLA,
	V_ID_N_TIPO_PERSONA,
	V_DE_CORREO,
	V_NU_TELEFONO,
	V_NU_DOCUMENTO,
	V_ID_PERSONA,
	V_FL_PRIMER_LOGIN,
	V_FL_C_ABOGADO,
	V_CO_USUARIO,
	V_FL_C_DEFENSOR
FROM
	SISCFEBF.CFTV_PERSONA cp
INNER JOIN SISCAS.SITV_CASILLA sc
			ON
	sc.ID_V_PERSONA = cp.ID_V_PERSONA
WHERE
	SC.CO_V_USUARIO = PI_USUARIO;

IF V_ES_CASILLA = 0 THEN
	PO_V_ERR_MSG := 'La casilla ' || PI_USUARIO || ' está desactivada';
	PO_V_ERR_COD := '1';
	RETURN ;
END IF ;

IF V_ID_N_TIPO_PERSONA = 1 OR V_ID_N_TIPO_PERSONA = 8 THEN
	 PO_V_ERR_MSG := 'NATURAL O ABOGADO';

OPEN PO_CURSOR FOR
			SELECT
	V_ID_CASILLA 		AS V_ID_CASILLA,
	V_ES_CASILLA 		AS V_ES_CASILLA ,
	V_FL_PRIMER_LOGIN 	AS V_FL_PRIMER_LOGIN,
	V_FL_C_ABOGADO 		AS V_FL_C_ABOGADO,
	V_ID_N_TIPO_PERSONA AS V_ID_N_TIPO_PERSONA,
	V_DE_CORREO 		AS V_DE_CORREO,
	V_NU_TELEFONO 		AS V_NU_TELEFONO,
	V_NU_DOCUMENTO 		AS V_NU_DOCUMENTO,
	V_ID_PERSONA 		AS V_ID_PERSONA,
	cpn.NO_V_CIUDADANO 	AS NO_V_CIUDADANO,
	cpn.AP_V_PATERNO 	AS AP_V_PATERNO,
	cpn.AP_V_MATERNO 	AS AP_V_MATERNO,
	V_CO_USUARIO		AS V_CO_USUARIO,
	V_FL_C_DEFENSOR		AS V_FL_C_DEFENSOR
FROM
	SISCFEBF.CFTV_PERSONA_NATURAL cpn
WHERE
	CPN.ID_V_PERSONA = V_ID_PERSONA;
END IF;

PO_V_ERR_COD := '0';

EXCEPTION
WHEN NO_DATA_FOUND THEN
    	PO_V_ERR_COD := '1';

PO_V_ERR_MSG := 'El usuario ingresado no se encuentra afiliado a la Casilla Fiscal Electrónica.';
WHEN OTHERS THEN
        PO_V_ERR_COD := '-1';

PO_V_ERR_MSG := SQLERRM || ' ' || SQLCODE;
END RECOVER_DATA;


PROCEDURE ACTUALIZAR_PASSWORD(  PI_USUARIO 		IN VARCHAR2,
								PI_NEW_PW 		IN VARCHAR2,
                         		PI_OLD_PW 		IN VARCHAR2,
                         		PO_V_ERR_COD 	OUT VARCHAR2,
    							PO_V_ERR_MSG 	OUT VARCHAR2
    							)  AS
    							V_ID_CASILLA SISCAS.SITV_CASILLA.ID_V_CASILLA%TYPE;

    BEGIN

	 	PO_V_ERR_MSG := 'OK';
		PO_V_ERR_COD := '0';

		SELECT ID_V_CASILLA INTO V_ID_CASILLA FROM SISCAS.SITV_CASILLA WHERE CO_V_USUARIO = PI_USUARIO;

		IF (PI_OLD_PW IS NULL) THEN
			UPDATE SISCAS.SITV_CASILLA
	   		SET PW_V_USUARIO = PKG_CE_SECURITY.encrypt_pw(PI_NEW_PW), FE_D_MODIFICACION = SYSDATE
	   		WHERE CO_V_USUARIO = PI_USUARIO ;
	   	ELSE
	   		UPDATE SISCAS.SITV_CASILLA
	   			SET PW_V_USUARIO = PKG_CE_SECURITY.encrypt_pw(PI_NEW_PW), FE_D_MODIFICACION = SYSDATE
	   			WHERE CO_V_USUARIO = PI_USUARIO
	   			AND PW_V_USUARIO = PKG_CE_SECURITY.encrypt_pw(PI_OLD_PW);

		END IF;

		IF SQL%ROWCOUNT = 0 THEN
            PO_V_ERR_COD := '1';
            PO_V_ERR_MSG := 'Los datos ingresados no son correctos.';
           RETURN;
        END IF;

       --- movimiento
			INSERT INTO SISCAS.SITV_MOVIMIENTO_CASILLA (
    			ID_V_CASILLA,
    			ID_N_TIPO_MOVIMIENTO_CASILLA,
    			DE_V_MOVIMIENTO_CASILLA,
    			CO_V_US_CREACION
			) VALUES (
    			V_ID_CASILLA,
    			1,
    			'ACTUALIZAR CONTRASEÑA',
    			PI_USUARIO
			);


	   EXCEPTION
    		WHEN NO_DATA_FOUND THEN
    			PO_V_ERR_COD := '1';
        		PO_V_ERR_MSG := 'Los datos ingresados no son correctos.';
    	WHEN OTHERS THEN
        	PO_V_ERR_COD := '-1';
        	PO_V_ERR_MSG := SQLERRM || ' ' || SQLCODE;

END ACTUALIZAR_PASSWORD;


PROCEDURE ACTUALIZAR_CORREO(
		PI_V_CORREO 		IN VARCHAR2,
		PI_V_ID_CASILLA		IN VARCHAR2,
        PO_V_ERR_COD 		OUT VARCHAR2,
    	PO_V_ERR_MSG 		OUT VARCHAR2
    )  AS
    V_CO_USUARIO SISCAS.SITV_CASILLA.CO_V_USUARIO%TYPE;
    BEGIN

	 PO_V_ERR_MSG := 'OK';
		PO_V_ERR_COD := '0';

		SELECT CO_V_USUARIO INTO V_CO_USUARIO FROM SISCAS.SITV_CASILLA WHERE ID_V_CASILLA = PI_V_ID_CASILLA;
			UPDATE SISCAS.SITV_CASILLA
	   		SET DE_V_CORREO = PI_V_CORREO, FE_D_MODIFICACION = SYSDATE
	   		WHERE ID_V_CASILLA = PI_V_ID_CASILLA ;

		IF SQL%ROWCOUNT = 0 THEN
            PO_V_ERR_COD := '-1';
            PO_V_ERR_MSG := 'Usuario inválido.';
           RETURN;

        END IF;

       --- movimiento
			INSERT INTO SISCAS.SITV_MOVIMIENTO_CASILLA (
    			ID_V_CASILLA,
    			ID_N_TIPO_MOVIMIENTO_CASILLA,
    			DE_V_MOVIMIENTO_CASILLA,
    			CO_V_US_CREACION
			) VALUES (
    			PI_V_ID_CASILLA,
    			3,
    			'ACTUALIZAR CORREO ELECTRONICO',
    			V_CO_USUARIO
			);


	   EXCEPTION
    		WHEN NO_DATA_FOUND THEN
    			PO_V_ERR_COD := '-1';
        		PO_V_ERR_MSG := 'Usuario inválido.';
    	WHEN OTHERS THEN
        	PO_V_ERR_COD := '-1';
        	PO_V_ERR_MSG := SQLERRM || ' ' || SQLCODE;

END ACTUALIZAR_CORREO;

PROCEDURE ACTUALIZAR_DOBLE_PERFIL(
								PI_V_ID_CASILLA 		IN VARCHAR2,
								PI_V_ID_PERSONA 		IN VARCHAR2,
								PI_V_DOBLE_PERFIL		IN VARCHAR2,
                         		PI_N_TIPO_CASILLA		IN NUMBER,
								PI_V_ID_COLEGIO 		IN VARCHAR2,
                         		PI_N_NU_COLEGIO 		IN VARCHAR2,
                         		PO_V_ERR_COD 			OUT VARCHAR2,
    							PO_V_ERR_MSG 			OUT VARCHAR2
    							)  AS
							V_COUNTABOGADO NUMBER;
							V_CO_USUARIO SISCAS.SITV_CASILLA.CO_V_USUARIO%TYPE;
BEGIN

	 	PO_V_ERR_MSG := 'OK';
		PO_V_ERR_COD := '0';

    	UPDATE SISCAS.SITV_CASILLA SET FL_C_ABOGADO = PI_V_DOBLE_PERFIL, FE_D_ABOGADO=SYSDATE WHERE ID_V_CASILLA = PI_V_ID_CASILLA;
    	SELECT CO_V_USUARIO INTO V_CO_USUARIO FROM SISCAS.SITV_CASILLA WHERE ID_V_CASILLA = PI_V_ID_CASILLA;
		-- abogado
		IF PI_V_DOBLE_PERFIL = '1'  THEN

			SELECT COUNT(ID_V_ABOGADO) INTO V_COUNTABOGADO
   				FROM SISCFEBF.CFTV_ABOGADO ca
   					WHERE ca.ID_V_PERSONA = PI_V_ID_PERSONA;
			IF V_COUNTABOGADO = 0 THEN
				INSERT INTO SISCFEBF.CFTV_ABOGADO (
				    ID_N_COLEGIO_ABOGADOS,
				    ID_V_PERSONA,
				    NU_V_COLEGIO,
				    CO_V_US_CREACION
				)
				VALUES (
				    PI_V_ID_COLEGIO,
				    PI_V_ID_PERSONA,
				    PI_N_NU_COLEGIO,
				    'USRSIS'
				);
			ELSE
				UPDATE SISCFEBF.CFTV_ABOGADO
					SET ID_N_COLEGIO_ABOGADOS=PI_V_ID_COLEGIO,
					NU_V_COLEGIO=PI_N_NU_COLEGIO
					WHERE ID_V_PERSONA=PI_V_ID_PERSONA;
			END IF;

		--- movimiento
			INSERT INTO SISCAS.SITV_MOVIMIENTO_CASILLA (
    			ID_V_CASILLA,
    			ID_N_TIPO_MOVIMIENTO_CASILLA,
    			DE_V_MOVIMIENTO_CASILLA,
    			CO_V_US_CREACION
			) VALUES (
    			PI_V_ID_CASILLA,
    			4,
    			'ACTIVAR DOBLE PERFIL',
    			V_CO_USUARIO
			);

        END IF;
	   EXCEPTION
    	WHEN OTHERS THEN
        	PO_V_ERR_COD := '-1';
        	PO_V_ERR_MSG := SQLERRM || ' ' || SQLCODE;

END ACTUALIZAR_DOBLE_PERFIL;

PROCEDURE CASP_USUARIOS_CON_PW_INICIAL(
				PO_CURSOR 				OUT SYS_REFCURSOR,
				PO_V_ERR_COD 			OUT VARCHAR2,
    			PO_V_ERR_MSG 			OUT VARCHAR2
    		) AS
BEGIN
	PO_V_ERR_COD := '0';
	PO_V_ERR_MSG := '';
	OPEN PO_CURSOR FOR

	SELECT SC.DE_V_CORREO, sc.ID_V_CASILLA, SC.CO_V_USUARIO, SC.FE_D_CREACION, SC.FL_C_PRIMER_LOGIN, CP.NO_V_CIUDADANO, CP.AP_V_PATERNO, CP.AP_V_MATERNO
			FROM SISCAS.SITV_CASILLA  sc
				INNER JOIN SISCFEBF.CFTV_PERSONA_NATURAL cp ON CP.ID_V_PERSONA  = SC.ID_V_PERSONA
				WHERE SC.FL_C_PRIMER_LOGIN = '0'
			AND MOD(TRUNC(SYSDATE)-TRUNC(SC.FE_D_CREACION), 30)=0
			AND TRUNC(SYSDATE) <> TRUNC(SC.FE_D_CREACION)
			AND SC.ES_C_CASILLA = '1';

	EXCEPTION
		WHEN OTHERS THEN
        	PO_V_ERR_COD := '-1';
			PO_V_ERR_MSG := SQLERRM || ' ' || SQLCODE;

END CASP_USUARIOS_CON_PW_INICIAL;


END CAPK_AFILIADO;